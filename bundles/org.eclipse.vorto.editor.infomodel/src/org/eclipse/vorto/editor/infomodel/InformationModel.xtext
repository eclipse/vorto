/*******************************************************************************
 * Copyright (c) 2014 Bosch Software Innovations GmbH and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * and Eclipse Distribution License v1.0 which accompany this distribution.
 *
 * The Eclipse Public License is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * The Eclipse Distribution License is available at
 * http://www.eclipse.org/org/documents/edl-v10.php.
 *
 * Contributors:
 * Bosch Software Innovations GmbH - Please refer to git log
 *
 *******************************************************************************/
grammar org.eclipse.vorto.editor.infomodel.InformationModel with org.eclipse.vorto.editor.datatype.Datatype

import "http://www.eclipse.org/vorto/metamodel/Datatype" as datatype
import "http://www.eclipse.org/vorto/metamodel/InformationModel"
import "http://www.eclipse.org/vorto/metamodel/Model" as model
import "http://www.eclipse.org/vorto/metamodel/Functionblock" as fbs

InformationModel:
	{InformationModel}
	'namespace' namespace = QualifiedName
    'version' version = VERSION
    'displayname' displayname=STRING      
    ('description' description=STRING)? 
    ('category' category=CATEGORY)?
    (references += ModelReference)*
	'infomodel' name=ID '{'
	('functionblocks' '{'
		(properties += FunctionblockProperty)*
	'}')?
	'}';

CATEGORY:
	ID ('/' ID)*;
	
QualifiedName:
	ID ('.' ID)*
;

FunctionblockProperty:
	(presence = Presence)? name = ID 'as' type = [fbs::FunctionblockModel|QualifiedName] (description=STRING)?
;

terminal VERSION : ('0'..'9')* '.' ('0'..'9')* '.' ('0'..'9')*('-'ID)?;

ModelReference returns model::ModelReference: 
	'using' importedNamespace=QualifiedName';'version=VERSION
;
