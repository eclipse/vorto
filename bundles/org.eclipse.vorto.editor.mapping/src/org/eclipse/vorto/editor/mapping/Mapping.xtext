/*******************************************************************************
 * Copyright (c) 2014 Bosch Software Innovations GmbH and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * and Eclipse Distribution License v1.0 which accompany this distribution.
 *
 * The Eclipse Public License is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * The Eclipse Distribution License is available at
 * http://www.eclipse.org/org/documents/edl-v10.php.
 *
 * Contributors:
 * Bosch Software Innovations GmbH - Please refer to git log
 *
 *******************************************************************************/
grammar org.eclipse.vorto.editor.mapping.Mapping with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/vorto/metamodel/Mapping"
import "http://www.eclipse.org/vorto/metamodel/InformationModel" as infomodel
import "http://www.eclipse.org/vorto/metamodel/Functionblock" as functionblock
import "http://www.eclipse.org/vorto/metamodel/Datatype" as datatype

MappingModel:
	'mapping' '{'
	'model' infomodel=[infomodel::InformationModel|QualifiedName]
	'target' target=ID
	(rules+=Rule)*
	'}';

Rule:
	'from' informationModelElements+=InformationModelElement (',' informationModelElements+=InformationModelElement)*
	'to' targetElement=TargetElement;

InformationModelElement:
	functionblockModel=[functionblock::FunctionblockModel|QualifiedName] '.' tail=FunctionBlockElement;

FunctionBlockElement:
	(OperationElement | ConfigurationElement | StatusElement | FaultElement | EventElement);

OperationElement:
	'operation.' value=[functionblock::Operation];

ConfigurationElement:
	'configuration.' value=[datatype::Property];

StatusElement:
	'status.' value=[datatype::Property];

FaultElement:
	'fault.' value=[datatype::Property];

EventElement:
	'event.' value=[functionblock::Event];

TargetElement:
	stereoTypes+=StereoType (',' stereoTypes+=StereoType)*;

StereoType:
	name=ID ('with {' attributes+=Attribute (',' attributes+=Attribute)* '}')?;

Attribute:
	name=ID ':' value=STRING;
	
QualifiedName:
	ID ('.' ID)*
;