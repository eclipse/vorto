/**
 * Copyright (c) 2020 Contributors to the Eclipse Foundation
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * https://www.eclipse.org/legal/epl-2.0
 *
 * SPDX-License-Identifier: EPL-2.0
 */
grammar org.eclipse.vorto.editor.datatype.Datatype with org.eclipse.xtext.common.Terminals

/*import "http://www.eclipse.org/emf/2002/Ecore" as ecore*/
import "http://www.eclipse.org/vorto/metamodel/Datatype"
import "http://www.eclipse.org/vorto/metamodel/Model"

Model:
	Type
;

ModelReference:
	'using' importedNamespace = QualifiedName';'version = VERSION
;

QualifiedName:
	ID ('.' ID)*
;

VortolangVersion:
  '1.0'
;

Type:
	Entity | Enum
;

Entity:
   'vortolang' lang=FLOAT
   'namespace' namespace = QualifiedName
   'version' version = VERSION
   
   (
   (('displayname' displayname=STRING)?) & 
   (('description' description=STRING)?)  &
   ('category' category=CATEGORY)?   
   )
   (references += ModelReference)*	
  'entity' name = ID ('extends' superType = [Entity|QualifiedName])? '{'
  	 (properties += Property)*
   '}'
;

Enum:
   'vortolang' lang=VortolangVersion type=[VortolangVersion]
   'namespace' namespace = QualifiedName
   'version' version = VERSION
   
   ((('displayname' displayname=STRING)?) & 
   (('description' description=STRING)?) &
   ('category' category=CATEGORY)?)     
   (references += ModelReference)*	
	'enum' name = ID '{'
		( enums += EnumLiteral (',' enums += EnumLiteral)* )?
	'}'
;

EnumLiteral:
	name = ID (description=STRING)?
;

Property:
	(extension ?= 'extension')? (presence = Presence)? (multiplicity ?= 'multiple')? name = ValidID 'as' type = PropertyType
	('with' '{' propertyAttributes+=PropertyAttribute (',' propertyAttributes+=PropertyAttribute)* '}')?
	('<' constraintRule = ConstraintRule '>')?
	(description=STRING)?
;

ConstraintRule:
	{ConstraintRule} (Constraints += Constraint ("," Constraints += Constraint)*)?	
;

PropertyAttribute:
  BooleanPropertyAttribute | EnumLiteralPropertyAttribute
;

BooleanPropertyAttribute:
  type=BooleanPropertyAttributeType ':' (value ?= 'true' | 'false')
;

enum BooleanPropertyAttributeType:
  readable = 'readable' |
  writable = 'writable' |
  eventable = 'eventable'
;

EnumLiteralPropertyAttribute:
  type=EnumLiteralPropertyAttributeType ':' value = [EnumLiteral | QualifiedName]
;

enum EnumLiteralPropertyAttributeType:
  measurementUnit = 'measurementUnit'
;

PropertyType:
	ObjectPropertyType | PrimitivePropertyType | ComplexPrimitivePropertyType
;

PrimitivePropertyType:
	type = PrimitiveType
;

ObjectPropertyType:
	type = [Type|QualifiedName]
;

ComplexPrimitivePropertyType:
	DictionaryPropertyType
;

DictionaryPropertyType:
	{DictionaryPropertyType} 'dictionary' ('[' keyType = PropertyType ',' valueType = PropertyType ']')?
;

enum PrimitiveType: string = 'string' | int = 'int' | float =  'float' | boolean =  'boolean' | datetime = 'dateTime' | double = 'double' | long = 'long' | short = 'short' | base64Binary ='base64Binary' | byte = 'byte';

Presence:
	 {Presence} (mandatory ?= 'mandatory' | 'optional')
;

Constraint:
	type = ConstraintIntervalType constraintValues = IntervalType
;

enum ConstraintIntervalType: min = 'MIN' | max = 'MAX' | strlen = 'STRLEN' | regex = 'REGEX' | mimetype = 'MIMETYPE' | scaling = 'SCALING' | default = 'DEFAULT' | nullable = 'NULLABLE' ; 

IntervalType:
	INT | SIGNEDINT | FLOAT | DATETIME | STRING | BOOLEAN
;

BOOLEAN:
	'true' | 'false'
;

CATEGORY: ID ('/' ID)*;

ValidID: ID | KEYWORD;

KEYWORD:
	'breakable' | 'category' | 'configuration' | 'dateTime' | 'description' | 'dictionary' | 'displayname' | 'events' |
	'extension' | 'fault' | 'functionblock' | 'functionblocks' | 'infomodel' | 'mandatory' | 'namespace' | 'operations' |
	'optional' | 'status' | 'version';

terminal SIGNEDINT :
	'-'INT
;

terminal FLOAT: ('^')?('-')?INT'.'INT;

terminal DATE: 
	('0'..'9')('0'..'9')('0'..'9')('0'..'9')('-')('0'..'9')('0'..'9')('-')('0'..'9')('0'..'9')
;

terminal TIME: 
	('0'..'9')('0'..'9')(':')('0'..'9')('0'..'9')(':')('0'..'9')('0'..'9')(TIMEZONE)?
;

terminal fragment TIMEZONE: 
	'Z'|(('+'|'-')('0'..'9')('0'..'9')(':')('0'..'9')('0'..'9'))
;

terminal DATETIME: 
	DATE('T')TIME
;

terminal VERSION : ('0'..'9')* '.' ('0'..'9')* '.' ('0'..'9')*('-'ID)?;